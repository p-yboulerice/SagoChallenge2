apply plugin: 'com.android.library'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
    }
    // Warning: Do not enable minify! Minify will merge unity classes.jar with the project's code and we fail
    // to remove it from the package.
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    productFlavors {
    }
}

dependencies {
    compile "com.android.support:support-core-utils:25.1.1"
    compile files(parent.projectDir.absolutePath + '/../.Unity/classes.jar')
}


// We remove the unity classes.jar from the final package (aar) since Unity Compiler is already including
// its own classes. This is to prevent error messages in Unity such as:
// Uncaught translation error: java.lang.IllegalArgumentException: already added: Lcom/unity3d/player/t;
android.libraryVariants.all { variant ->
    variant.outputs.each { output ->
        output.packageLibrary.exclude('libs/classes.jar')
    }
}

gradle.buildFinished {
    copy {
        from(buildDir.absolutePath + '/outputs/aar/') {
            include 'sagoApp-release.aar'
        }
        into(parent.projectDir.absolutePath + '/dist')
    }
}